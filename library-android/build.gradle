plugins {
    id "com.android.library"
    id "maven-publish"
    id 'signing'
}

version = "2.1"
group "de.westnordost"

repositories {
    mavenLocal()
    mavenCentral()
    google()
    jcenter {
        content {
            //  org.jetbrains.trove4j is only available in JCenter
            includeGroup("org.jetbrains.trove4j")
        }
    }
}

dependencies {
    //api (project(':library')) {
    api ('de.westnordost:osmfeatures:2.1') {
        // it's already included in Android
        exclude group: 'org.json', module: 'json'
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 29
        versionCode 1
        versionName project.version
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact("$buildDir/outputs/aar/library-android-release.aar")
            artifact sourcesJar
            artifact javadocJar
            artifactId "osmfeatures-android"
            pom {
                name = 'osmfeatures-android'
                description = 'Android library to translate OSM tags to and from localized names.'
                url = 'https://github.com/westnordost/osmfeatures'
                scm {
                    connection = 'https://github.com/westnordost/osmfeatures.git'
                    developerConnection = 'https://github.com/westnordost/osmfeatures.git'
                    url = 'https://github.com/westnordost/osmfeatures'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'westnordost'
                        name = 'Tobias Zwick'
                        email = 'osm@westnordost.de'
                    }
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.api.allDependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "mavenCentral"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}